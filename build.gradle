plugins {
    id 'net.minecrell.plugin-yml.bukkit' version '0.6.0'
    id("xyz.jpenilla.run-paper") version "2.3.1"
    id 'com.gradleup.shadow' version '9.0.0-beta4'

    id "io.papermc.paperweight.userdev" version "2.0.0-beta.10" apply false
}

group = 'org.eu.pcraft'
version = '1.5'
dev = project.dev

allprojects {
    apply plugin: "java"
    apply plugin: "java-library"

    java {
        sourceCompatibility = JavaVersion.toVersion(21)
        targetCompatibility = JavaVersion.toVersion(21)
        toolchain.languageVersion = JavaLanguageVersion.of(21)
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
    }

    repositories {
        mavenCentral()
        maven {
            name = "papermc-repo"
            url = "https://repo.papermc.io/repository/maven-public/"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/groups/public/"
        }
        maven {
            name = "CodeMC"
            url = uri("https://repo.codemc.io/repository/maven-public/")
        }
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        // lombok
        compileOnly 'org.projectlombok:lombok:1.18.36'
        annotationProcessor 'org.projectlombok:lombok:1.18.36'
        testCompileOnly 'org.projectlombok:lombok:1.18.36'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.36'
        implementation 'org.spongepowered:configurate-yaml:4.2.0-SNAPSHOT'
        implementation 'org.spongepowered:configurate-core:4.2.0-SNAPSHOT'
    }
}

subprojects {
    group = rootProject.group
    version = rootProject.version
}

configurations {
    include
    implementation.extendsFrom(include)
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.19.4-R0.1-SNAPSHOT'
    include 'org.bstats:bstats-bukkit:3.0.2'
    compileOnly "com.github.MilkBowl:VaultAPI:1.7"

    include project(":nms")
    for (String version : new String[]{"v1_21_4"}) {    // versions with remapper
        include project(path: ":nms:$version")
    }
    for (String version : new String[]{"v1_20_4", "v1_20_2", "v1_19_4", "v1_18_2"}) {   // versions without remapper
        include project(path: ":nms:$version", configuration: "reobf")
    }

    include 'cn.afternode.commons:bukkit:2.7.1'
    compileOnly("de.tr7zw:item-nbt-api-plugin:2.15.5")
}

bukkit {
    name = "PowerfulFireworks"
    main = "org.eu.pcraft.powerfulfireworks.PowerfulFireworks"
    apiVersion = "1.18"
    depend = ['Vault']
    softDepend = ['NBTAPI']
}

runServer {
    minecraftVersion(project.property("run.version") as String)

    downloadPlugins {
        modrinth("nbtapi", "2.15.5")
    }
}

tasks.withType(xyz.jpenilla.runtask.task.AbstractRun) {
    javaLauncher = javaToolchains.launcherFor {
        vendor = JvmVendorSpec.JETBRAINS
        languageVersion = JavaLanguageVersion.of(21)
    }
    jvmArgs("-XX:+AllowEnhancedClassRedefinition")
}

compileJava {
    options.release = 17
}

shadowJar {
    configurations = [project.configurations.include]

    dependencies {
        exclude(dependency("net.kyori:option"))
    }

    relocate "org.bstats", "org.eu.pcraft.powerfulfireworks.bstats"
    def dev = Boolean.parseBoolean(project.getProperties().get("dev").toString())
    println "Dev: ${dev}"
    if (!dev) {
        println "Relocation enabled"
        for (var s in new String[]{"cn.afternode", "io.leangen", "org.spongepowered"}) {
            relocate s, "org.eu.pcraft.powerfulfireworks.libs.${s}"
            println "relocate $s -> org.eu.pcraft.powerfulfireworks.${s}"
        }
    }
    manifest {
        attributes["paperweight-mappings-namespace"] = "spigot"
    }
}

tasks.register("downloadVanillaServer", Exec) {
    def version = project.property("run.version") as String
    it.group "run paper"
    commandLine("curl", "-L", "https://bmclapi2.bangbang93.com/version/$version/server", "-o", "run/cache/mojang_${version}.jar")

    doFirst {
        def cacheDir = project.layout.projectDirectory.dir("run").dir("cache").asFile

        if (!cacheDir.exists())
            cacheDir.mkdirs()
    }
}
